/*****************************************************************************
 *         plugins                                                          *
 *****************************************************************************/

plugins {
    id 'java-library'
    id 'checkstyle'
    id 'idea'
    id 'com.github.spotbugs'
    id 'jacoco'
    id "org.sonarqube"
    id 'com.github.dawnwords.jacoco.badge'
}


/*****************************************************************************
 *         group and version for java projects                               *
 *****************************************************************************/

group = 'io.github.stewseo'
version = '1.7.4'

/*****************************************************************************
 *         packaging                                                         *
 *****************************************************************************/

java {
    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17
}

jar {

    archiveFileName.set(project.name)
    archiveVersion.set(version)

    manifest {
        attributes("Implementation-Title": archiveFileName,
                "Implementation-Version": archiveVersion)
    }

    from {
        configurations.compileClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

tasks.withType(JavaCompile) {
    javaCompiler.set(javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    })
    options.deprecation = true
    options.encoding = 'UTF-8'
}


/*****************************************************************************
 *         checkstyle                                                       *
 *****************************************************************************/

// No Warnings
checkstyle {
    config = resources.text.fromString(io.github.stewseo.CheckstyleUtil.getCheckstyleConfig("$rootDir/config/checkstyle/checkstyle.xml"))
    maxWarnings = 1
}

/*****************************************************************************
 *         spot bugs                                                        *
 *****************************************************************************/

spotbugs {
    ignoreFailures = true
    showStackTraces = true
    showProgress = false
    // adjusts internal flags of SpotBugs to reduce computation cost by lowering the prediction. min, max, more, max
    effort = 'default'
    reportsDir = file("${buildDir}/reports/spotbugs")
    includeFilter = file("${rootDir}/config/spotbugs/spotbugs-include.xml")
    excludeFilter = file("${projectDir}/config/spotbugs/spotbugs-exclude.xml")
}


/*****************************************************************************
 *         code coverage                                                     *
 *****************************************************************************/

jacoco {
    toolVersion = "0.8.8"
}

sonarqube {
    properties {
        property 'sonar.host.url', 'http://localhost:9000'
    }
}

test.finalizedBy jacocoTestReport

tasks.named('sonarqube').configure {
    dependsOn test
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

/*****************************************************************************
 *         java-conventions dependencies                                   *
 *****************************************************************************/

repositories {
    mavenCentral()
}

dependencies {

    configurations.all {
        exclude group: "commons-logging", module: "commons-logging"
    }

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.1'

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation buildLibs.assertJ
    testImplementation buildLibs.junit.jupiter.api
    testRuntimeOnly buildLibs.junit.jupiter.engine
}

/*****************************************************************************
 *         tasks to print project pathing                                   *
 *****************************************************************************/

tasks.register('printPaths') {
    doLast {
        println "Project name: " + project.name +
                "\nrootDir.path: " + rootDir.path +
                "\nprojectDir path: " + projectDir.path +
                "\nprojectDir: " + projectDir +
                "\nbuildDir path: " + buildDir.path
    }
}
