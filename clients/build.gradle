/*****************************************************************************
 *         plugins                                                      *
 *****************************************************************************/

plugins {
    id 'library-conventions'
}

apply from: "${rootDir}/config/maven-publish/maven-publish.gradle"

/*****************************************************************************
 *         java                                                             *
 *****************************************************************************/

/*****************************************************************************
 *         :clients dependencies                                            *
 *****************************************************************************/

dependencies {
//    api files("../libs/lowlevel-restclient-1.8.0.jar")
//    api files("../libs/temporaldata-service-1.8.0.jar")
    api project(":lowlevel-restclient")
    api project(":temporaldata-service")

    implementation buildLibs.elasticsearch.java

    testImplementation 'org.mockito:mockito-junit-jupiter:5.0.0'

    api buildLibs.jakarta.json.processing
    api buildLibs.jakarta.json.bind
    api buildLibs.jackson.core
    api buildLibs.jackson.databind
    api buildLibs.eclipse.parsson

    api buildLibs.ggjte

    testImplementation buildLibs.classgraph

    testImplementation("org.eclipse:yasson:2.0.4") {
        // Exclude Glassfish as we use Parsson (basically Glassfish renamed in the Jakarta namespace).
        exclude(group: "org.glassfish", module: "jakarta.json")
    }
}

/*****************************************************************************
 *         :testing                                                         *
 *****************************************************************************/


test {

    useJUnitPlatform {
        includeTags "util", "json", "_types","transport", "yelpfusion", "elasticsearch"
    }
}

/*****************************************************************************
 *         publishing                                                       *
 *****************************************************************************/

publishing {
    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = POM_NAME
                artifactId = POM_ARTIFACT_ID
                setGroupId POM_GROUP_ID
                packaging = POM_PACKAGING
                description = POM_DESCRIPTION
                url = POM_URL

                licenses {
                    license {
                        name = POM_LICENSE_NAME
                        url = POM_LICENSE_URL
                    }
                }

                scm {
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                    url = POM_SCM_URL
                }

                developers {
                    developer {
                        id = DEV_ID
                        name = DEV_NAME
                        email = DEV_EMAIL
                    }
                }
                withXml {
                    asNode().dependencies.dependency.each { dep ->
                        if (dep.artifactId.last().value().last() in ["elasticsearch-java"]) {
                            assert dep.parent().remove(dep)
                        }
                    }
                }
            }


        }
    }
}


signing {
    sign publishing.publications.mavenJava
}

